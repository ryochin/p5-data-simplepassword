#!/usr/bin/perl --
# 
# $Id$

use strict;
use Getopt::Std;
use Pod::Usage;
use List::Util qw(first);
use Data::SimplePassword;

# getopts
Getopt::Std::getopts 'ht:n:' => my $opt = {};

# extra args
&usage if scalar @ARGV;

# help
&usage if defined $opt->{h};

# length
my $n = $opt->{n} || 8;
&usage unless $n =~ /^\d+$/o;

# type
my $chars = {
    default => [ 0..9, 'a'..'z', 'A'..'Z' ],
    base64 => [ 0..9, 'a'..'z', 'A'..'Z', qw(+ /) ],
    simple => [ 0..9, 'a'..'z' ],
    digit => [ 0..9 ],
    binary => [ 0, 1 ],
    morse => [ qw(. -) ],
};

my $type = $opt->{t} || 'default';
&usage if not  first { $type eq $_ } keys %{ $chars };

my $sp = Data::SimplePassword->new;
$sp->chars( @{ $chars->{ $type } } );

print $sp->make_password( $n ), "\n";

sub usage {
    pod2usage 0;
}

*HELP_MESSAGE = \&usage;

__END__

=head1 NAME

rndpassword - a command-line frontend to Data::SimplePassword

=head1 SYNOPSIS

rndpassword [-h] [-t <type>] [-n <length>]

 Options:
    -h  show usage
    -t  set charactor type
    -n  set length (default: 8 byte)

 Types:
    default  0xaVbi3O2Lz8E69s
    base64   PC2gb5/8+fBDuw+d
    simple   xek4imbjcmctsxd3
    digit    7563919623282657
    binary   1001011110000101
    morse    -.--...-.--.-..-

 Examples:
    shell> rndpassword
    shell> rndpassword -n 12
    shell> rndpassword -t digit -n 32
    shell> rndpassword -t binary -n 1024

=head1 DEPENDENCY

Data::SimplePassword

